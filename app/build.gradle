plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def sdkVersion = "0.2.3"

Properties localProps = getLocalProperties()

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.particle.demo"
        minSdk 23
        targetSdk 31
        versionCode 2
        versionName "$sdkVersion"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [
                PN_PROJECT_ID        : localProps['particle.network.project_id'],
                PN_PROJECT_CLIENT_KEY: localProps['particle.network.project_client_key'],
                PN_APP_ID            : localProps['particle.network.app_id']
        ]
        ndk {
            abiFilters.add("armeabi-v7a")
            abiFilters.add("arm64-v8a")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    buildFeatures {
        dataBinding = true
    }
}

def getLocalProperties() {
    Properties props = new Properties()
    props.load(project.rootProject.file('local.properties').newDataInputStream())
    return props
}

dependencies {
    //particle auth service
    implementation "network.particle:auth-service:$sdkVersion"
    //particle api service
    implementation "network.particle:api-service:$sdkVersion"
    //particle wallet service
    implementation "network.particle:wallet-service:$sdkVersion"

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'com.blankj:utilcodex:1.31.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}